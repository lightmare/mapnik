@echo off
SET EL=0

set MAPNIK_VERSION={{MAPNIK_VERSION}}
set MAPNIK_VERSION_NUMBER={{MAPNIK_VERSION_NUMBER}}
::SET CUR_PATH=%CD%
::cd %CD%/../
set MAPNIK_PREFIX=%~dp0
:: strip trailing \
set MAPNIK_PREFIX=%MAPNIK_PREFIX:~0,-1%
:: get dirname
for %%F in (%MAPNIK_PREFIX%) do set MAPNIK_PREFIX=%%~dpF
:: strip trailing \
set MAPNIK_PREFIX=%MAPNIK_PREFIX:~0,-1%
:: now make double \\ for gyp
set MAPNIK_PREFIX=%MAPNIK_PREFIX:\=\\%
set MAPNIK_LIBS=%MAPNIK_PREFIX%\\lib
set MAPNIK_INCLUDES=%MAPNIK_PREFIX%\\include
set MAPNIK_INPUT_PLUGINS_DIRECTORY=%MAPNIK_PREFIX%\\lib\\mapnik\\input
set MAPNIK_FONTS_DIRECTORY=%MAPNIK_PREFIX%\\lib\\mapnik\\fonts

if /i "%1"=="" (
  SET EL=1
  goto help_msg
)

:Loop
IF "%1"=="" GOTO Continue

    if /i "%1"=="-v" (
      echo %MAPNIK_VERSION%
      goto exit
    )

    if /i "%1"=="--version" (
      echo %MAPNIK_VERSION%
      goto exit
    )

    if /i "%1"=="--version-number" (
      echo %MAPNIK_VERSION_NUMBER%
      goto exit
    )

    if /i "%1"=="--git-revision" (
      echo {{GIT_REVISION}}
      goto exit
    )

    if /i "%1"=="--git-describe" (
      echo {{GIT_DESCRIBE}}
      goto exit
    )

    if /i "%1"=="help" (
      goto help_msg
    )

    if /i "%1"=="--help" (
      goto help_msg
    )

    if /i "%1"=="-help" (
      goto help_msg
    )

    if /i "%1"=="-h" (
      goto help_msg
    )

    if /i "%1"=="/help" (
      goto help_msg
    )

    if /i "%1"=="?" (
      goto help_msg
    )

    if /i "%1"=="-?" (
      goto help_msg
    )

    if /i "%1"=="--?" (
      goto help_msg
    )

    if /i "%1"=="/?" (
      goto help_msg
    )

    set hit=""

    if /i "%1"=="--prefix" (
      echo %MAPNIK_PREFIX%
      set hit=%1
    )

    if /i "%1"=="--input-plugins" (
      echo %MAPNIK_INPUT_PLUGINS_DIRECTORY%
      set hit=%1
    )

    if /i "%1"=="--fonts" (
      echo %MAPNIK_FONTS_DIRECTORY%
      set hit=%1
    )

    if /i "%1"=="--lib-name" (
      echo mapnik
      set hit=%1
    )

    if /i "%1"=="--libs" (
      echo mapnik.lib
      set hit=%1
    )

    @rem TODO - figure out how to avoid hardcoding these library names
    if /i "%1"=="--dep-libs" (
      echo {{DEP_LIBS}}
      set hit=%1
    )

    if /i "%1"=="--ldflags" (
      echo %MAPNIK_LIBS%
      set hit=%1
    )

    if /i "%1"=="--defines" (
      echo _WINDOWS BOOST_ALL_NO_LIB BOOST_MSVC_ENABLE_2014_JUN_CTP BOOST_VARIANT_DO_NOT_USE_VARIADIC_TEMPLATES HAVE_JPEG HAVE_PNG HAVE_WEBP HAVE_TIFF MAPNIK_USE_PROJ4 BOOST_REGEX_HAS_ICU GRID_RENDERER SVG_RENDERER MAPNIK_THREADSAFE BIGINT HAVE_LIBXML2 HAVE_CAIRO LIBXML_STATIC
      set hit=%1
    )

    @rem /MD is multithreaded dynamic linking - http://msdn.microsoft.com/en-us/library/2kzt1wy3.aspx
    @rem /EHsc is to support c++ exceptions - http://msdn.microsoft.com/en-us/library/1deeycx5(v=vs.80).aspx
    @rem /GR is to support rtti (runtime type detection) - http://msdn.microsoft.com/en-us/library/we6hfdy0.aspx
    if /i "%1"=="--cxxflags" (
      echo /MD /EHsc /GR
      set hit=%1
    )

    if /i "%1"=="--includes" (
      echo %MAPNIK_INCLUDES% %MAPNIK_INCLUDES%\\mapnik\\agg
      set hit=%1
    )

    if /i "%1"=="--all-flags" (
      @rem nothing here yet
      echo ""
      set hit=%1
    )

    if /i "%1"=="--cxx" (
      @rem nothing here yet
      echo ""
      set hit=%1
    )

    if /i "%1"=="--cflags" (
      @rem nothing here yet
      echo ""
      set hit=%1
    )

    if /i "%1"=="--dep-includes" (
      @rem nothing here yet
      echo %MAPNIK_INCLUDES%\\cairo %MAPNIK_INCLUDES%\\freetype2 %MAPNIK_INCLUDES%\\google %MAPNIK_INCLUDES%\\libxml2
      set hit=%1
    )

    @rem if we got here print warning
    if /i NOT %1==%hit% (
      echo unknown option %1 1>&2
      SET EL=1
    )
SHIFT
GOTO Loop
:Continue


goto exit

:help_msg
echo Usage: mapnik-config
echo Examples:
echo   -h --help         : display this help and exit
echo   -v --version      : version information (MAPNIK_VERSION_STRING)
echo   --version-number  : version number (MAPNIK_VERSION) (new in 2.2.0)
echo   --git-revision    : git hash from "git rev-list --max-count=1 HEAD"
echo   --git-describe    : git decribe output (new in 2.2.0)
echo   --libs            : provide lib name for mapnik.dll
echo   --defines         : provide compiler defines needed for this mapnik build
echo   --dep-libs        : provide lib names of depedencies
echo   --ldflags         : provide lib paths to depedencies
echo   --cxxflags        : provide compiler flags
echo   --includes        : provide header paths for mapnik
echo   --dep-includes    : provide header paths for dependencies
echo   --input-plugins   : provide path to input plugins directory
echo   --fonts           : provide path to fonts directory

:EXIT
exit /b %EL%
